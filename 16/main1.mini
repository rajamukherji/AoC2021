let Hex := file(Args[1], "r"):rest:trim

let Buffer := string::buffer()
for C in Hex do
	let S := string(integer(C, 16), 2)
	for I in S:length .. 3 do
		Buffer:write("0")
	end
	Buffer:write(S)
end

let Bits := Buffer:rest
print('Bits = {Bits}\n')
Buffer:write(Bits)

fun decode(Buffer) do
	let Version := integer(Buffer:read(3), 2)
	let Type := integer(Buffer:read(3), 2)
	print('Version = {Version}, Type = {Type}\n')
	if Type = 4 then
		var Value := 0
		loop
			while Buffer:read(1) = "1"
			Value := (old << 4) + integer(Buffer:read(4), 2)
		end
		Value := (old << 4) + integer(Buffer:read(4), 2)
		print('Value = {Value}\n')
		ret [Version, Type, Value]
	else
		let Packet := [Version, Type]
		if Buffer:read(1) = "1" then
			let Count := integer(Buffer:read(11), 2)
			print('Count = {Count}\n')
			for I in 1 .. Count do
				Packet:put(decode(Buffer))
			end
		else
			let Length := integer(Buffer:read(15), 2)
			let Start := Buffer:length
			print('Length = {Length}\n')
			loop
				Packet:put(decode(Buffer))
				print('Start = {Start}, Buffer = {Buffer:length}\n')
				while (Start - Buffer:length) < Length
			end
		end
		ret Packet
	end
end

let Packet := decode(Buffer)
print(Packet, "\n")

fun versions(Packet) do
	if Packet in list then
		ret Packet[1] + sum(Packet skip 2, versions)
	else
		ret 0
	end
end

print(versions(Packet), "\n")
